plugins {
    id "com.github.johnrengelman.shadow" version "5.0.0"
    id 'maven-publish'
    id "com.matthewprenger.cursegradle"
}

configurations {
    shadow
}

architectury {
    platformSetupLoomIde()
}

repositories {
    mavenCentral()
    maven {
        url = "https://dl.bintray.com/oroarmor/oroarmor"
    }
}

loom {
    mixinConfigs = ["netherite-plus-common-forge.mixins.json", "multi-item-lib.mixins.json", "netherite-plus-forge.mixins.json"]
    useFabricMixin = true
}

dependencies {
    minecraft("com.mojang:minecraft:${rootProject.architectury.minecraft}")
    mappings("net.fabricmc:yarn:${rootProject.mappings}:v2")
    forge("net.minecraftforge:forge:${rootProject.architectury.minecraft}-${rootProject.forge_version}")

    modCompile("me.shedaniel:architectury:${rootProject.architectury_version}:forge") {
        exclude(module: "fabric-loader")
    }

    modCompile('com.oroarmor:oro-config-forge:2.0.0') {
        exclude(module: "fabric-loader")
    }

    shadow('com.oroarmor:oro-config-forge:2.0.0') {
        exclude(module: "fabric-loader")
        exclude(module: "modmenu")
        exclude(module: "config-2")
        exclude(module: "fabric-command-api-v1")
    }

    compileOnly(project(path: ":common")) {
        exclude(module: "fabric-loader")
        transitive = false
    }
    runtimeOnly(project(path: ":common", configuration: "transformDevelopmentForge")) {
        exclude(module: "fabric-loader")
        transitive = false
    }
    shadow(project(path: ":common", configuration: "transformProductionForge")) {
        exclude(module: "fabric-loader")
        transitive = false
    }
}

processResources {
    inputs.property "version", rootProject.version

    from(sourceSets.main.resources.srcDirs) {
        include "META-INF/mods.toml"
        expand "version": rootProject.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude "META-INF/mods.toml"
    }
}


shadowJar {
    relocate "com.oroarmor.netherite_plus", "com.oroarmor.netherite_plus.forge"
    relocate "com.oroarmor.config", "com.oroarmor.config.forge"

    exclude "fabric.mod.json"

    configurations = [project.configurations.shadow]
    classifier "shadow"
}

remapJar {
    dependsOn(shadowJar)
    input.set(shadowJar.archivePath)
    archiveClassifier = "forge"
}

publishing {
    publications {
        mavenForge(MavenPublication) {
            artifact(remapJar.archivePath) {
                builtBy build
                classifier "forge"
            }

            groupId project.group
            artifactId project.archivesBaseName
            version project.version
        }
    }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    publications = ["mavenForge"]
    publish = true
    pkg {
        repo = 'oroarmor'
        name = rootProject.archives_base_name
        userOrg = "oroarmor"
        licenses = ['CC0-1.0']
        vcsUrl = 'https://github.com/OroArmor/Netherite-Plus-Mod.git'
        version {
            name = rootProject.mod_version + "-" + rootProject.minecraft_version
        }
    }
}

curseforge {
    if (System.getenv("CURSE_API_KEY") != null) {
        apiKey = System.getenv("CURSE_API_KEY")
        project {
            id = "394120"
            releaseType = "release"
            addGameVersion "1.16-Snapshot"
            addGameVersion "1.16.4"
            addGameVersion "1.16.5"
            addGameVersion "Java 8"
            addGameVersion "Forge"
            changelogType = "html"
            changelog = rootProject.ext.releaseChangelog
            relations {
                requiredDependency "architectury-forge"
            }
            mainArtifact(remapJar.archivePath) {
                displayName = "Netherite Plus Mod - $rootProject.mod_version - Forge"
            }
            afterEvaluate {
                uploadTask.dependsOn("build")
            }
        }
    }
    options {
        forgeGradleIntegration = false
        javaVersionAutoDetect = false
    }
}

import com.modrinth.minotaur.TaskModrinthUpload

task modrinthForge (type: TaskModrinthUpload){
    token = System.getenv("MODRINTH_TOKEN") // Use an environment property!
    projectId = 'MlaM22M4'
    versionNumber = project.version + " - Forge"
    uploadFile = remapJar.archivePath // This is the java jar task
    changelog = rootProject.ext.releaseChangelog
    addGameVersion('1.16.4')
    addGameVersion('1.16.5')
    addLoader('forge')
    doLast {
        if (wasUploadSuccessful()) {
            rootProject.ext.forgeModrinthDownloadLink = "https://modrinth.com/mod/netherite-plus-mod/version/$uploadInfo.id"
            sendDiscordWebhook()
        }
    }
}