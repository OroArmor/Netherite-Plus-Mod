plugins {
    id "com.github.johnrengelman.shadow" version "5.0.0"
    id "com.matthewprenger.cursegradle"
}

minecraft {
    accessWidener = file("src/main/resources/netherite_plus.accesswidener")
}

configurations {
    shadow
}

architectury {
    platformSetupLoomIde()
    fabric()
}

repositories {
    mavenCentral()
    maven {
        name = "Fabric"
        url = "https://maven.fabricmc.net/"
    }
    maven {
        url = 'https://maven.modmuss50.me/'
    }
    maven {
        url = "https://jitpack.io"
    }
    maven {
        name = "NerdHubMC"
        url = "https://maven.abusedmaster.xyz/"
    }
//    maven { url = "https://dl.bintray.com/kyrptonaught/Quickshulker/" }
//    maven { url = "https://dl.bintray.com/kyrptonaught/kyrptconfig/" }
    maven { url = "https://maven.misterpemodder.com/libs-release/" }
    maven {
        url = "http://maven.terraformersmc.com/releases/"
    }
    maven {
        url "https://www.cursemaven.com"
        content {
            includeGroup "curse.maven"
        }
    }
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

def runAndCompile(List<String> mods, boolean run) {
    dependencies {
        mods.each {
            modCompileOnly(it) {
                exclude(module: "fabric-api")
                exclude(module: "modmenu")
            }
            if (run) {
                modRuntime (it) {
                    exclude(module: "modmenu")
                    exclude(module: "fabric-api")
                }
            }
        }
    }
}

def includeLibraries(List<String> libraries) {
    dependencies {
        libraries.each {
            modImplementation (it) {
                exclude(module: "modmenu")
                exclude(module: "fabric-api")
            }
            include (it) {
                exclude(module: "modmenu")
                exclude(module: "fabric-api")
            }
        }
    }
}

def runAlways(List<String> mods) {
    dependencies {
        mods.each {
            modRuntime (it) {
                exclude(module: "modmenu")
                exclude(module: "fabric-api")
            }
        }
    }
}
sourceSets {
    main {
        java {
            exclude "com/oroarmor/netherite_plus/mixin/better_end"
        }
    }
}

dependencies {

    //to change the versions see the gradle.properties file
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings("net.fabricmc:yarn:${rootProject.mappings}:v2")
    modImplementation "net.fabricmc:fabric-loader:${rootProject.fabric_loader_version}"

    // Fabric API. This is technically optional, but you probably want it anyway.
    modCompile "net.fabricmc.fabric-api:fabric-api:${rootProject.fabric_version}"

    //Libraries
    includeLibraries(['com.oroarmor:oro-config-fabric:3.0.0',
                      'com.oroarmor:multi-item-lib:1.3.0'])

    runAndCompile([/*"net.kyrptonaught:quickshulker:${project.quickshulker}",*/
                   "com.github.emilyploszaj:trinkets:${project.trinkets}",
                   "com.misterpemodder:shulkerboxtooltip:${project.shulkerboxtooltip}+"+/*${project.minecraft_version}*/"1.16.4",
    ], true)

//    modCompileOnly "curse.maven:betterend-413596:3168267"

    modCompile("com.terraformersmc:modmenu:${project.modmenu}") {
        exclude(module: "fabric-api")
    }

    runAlways(["me.shedaniel:RoughlyEnoughItems:${project.rei}"])

    modCompileOnly("me.shedaniel:RoughlyEnoughItems-api:${project.rei}") {
        exclude(module: "fabric-api")
        exclude(module: "modmenu")
    }

    modCompile("me.shedaniel:architectury-fabric:${rootProject.architectury_version}") {
        exclude(module: "fabric-api")
        exclude(module: "modmenu")
    }

    include("me.shedaniel:architectury-fabric:${rootProject.architectury_version}") {
        exclude(module: "fabric-api")
        exclude(module: "modmenu")
    }

    implementation(project(path: ":common")) {
        transitive = false
        exclude(module: "fabric-api")
    }
    developmentFabric(project(path: ":common")) {
        transitive = false
        exclude(module: "fabric-api")
    }
    shadow(project(path: ":common", configuration: "transformProductionFabric")) {
        transitive = false
        exclude(module: "fabric-api")
    }
}

processResources {
    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
    inputs.property "version", project.version
}

shadowJar {
    configurations = [project.configurations.shadow]
    classifier "shadow"
}

remapJar {
    dependsOn(shadowJar)
    input.set(shadowJar.archivePath)
    archiveClassifier = "fabric"
}

processResources {
    inputs.property "version", project.version

    from(sourceSets.main.resources.srcDirs) {
        include "fabric.mod.json"
        expand "version": project.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude "fabric.mod.json"
    }
}

// ensure that the encoding is set to UTF-8, no matter what the system default is
// this fixes some edge cases with special characters not displaying correctly
// see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
// if it is present.
// If you remove this task, sources will not be generated.
task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = "sources"
    from sourceSets.main.allSource
}

jar {
    from "LICENSE"
}

// configure the maven publication
publishing {
    publications {
        mavenFabric(MavenPublication) {
            artifact(remapJar.archivePath) {
                builtBy build
                classifier "fabric"
            }

            groupId project.group
            artifactId project.archivesBaseName
            version project.version
        }
    }
}

curseforge {
    if (System.getenv("CURSE_API_KEY") != null) {
        apiKey = System.getenv("CURSE_API_KEY")
        project {
            id = "394120"
            releaseType = "release"
            addGameVersion "1.16-Snapshot"
            addGameVersion "1.16.4"
            addGameVersion "1.16.5"
            addGameVersion "Java 8"
            addGameVersion "Fabric"
            changelogType = "html"
            changelog = rootProject.ext.releaseChangelog
            relations {
                requiredDependency "fabric-api"
            }
            mainArtifact(remapJar.archivePath) {
                displayName = "Netherite Plus Mod - $rootProject.mod_version - Fabric"
            }
            afterEvaluate {
                uploadTask.dependsOn("build")
            }
        }
    }
    options {
        forgeGradleIntegration = false
        javaVersionAutoDetect = false
    }
}

import com.modrinth.minotaur.TaskModrinthUpload

task modrinthFabric (type: TaskModrinthUpload){
    token = System.getenv("MODRINTH_TOKEN") // Use an environment property!
    projectId = 'MlaM22M4'
    versionNumber = project.version + " - Fabric"
    uploadFile = remapJar.archivePath // This is the java jar task
    changelog = rootProject.ext.releaseChangelog
    addGameVersion('1.16.4')
    addGameVersion('1.16.5')
    addLoader('fabric')

    doLast {
        if (wasUploadSuccessful()) {
            rootProject.ext.fabricModrinthDownloadLink = "https://modrinth.com/mod/netherite-plus-mod/version/$uploadInfo.id"
            sendDiscordWebhook()
        }
    }
}
